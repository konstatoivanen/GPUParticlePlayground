#pragma kernel CSBoidMain

struct Boid
{
  float2 pos;
  float2 vel;
  float4 col;
};

int    NumBoids;
float4 Params1;
float4 Params2;
float4 Params3;
float4 Params4;
float4 Params5;

RWStructuredBuffer<Boid> BoidBuffer;
Texture2D<float4>        CollisionMask;
SamplerState             samplerCollisionMask;

#define G (6.67300E-11) //Gravitational Constant G
#define DYNAMIC_FRICTION_COEF (0.95f)

#define REPEL_WEIGHT     (Params1.x) //Weight of the boid repel force.
#define ALIGN_WEIGHT     (Params1.y) //Weight of the boid align force.
#define COHESION_WEIGHT  (Params1.z) //Weight of the boid cohesion force.
#define COHESION_COEF    (Params1.w) //Coefficient for cohesion / velocity substraction.

#define REPEL_DIST       (Params2.x) //Distance threshold under which boid repelling is applied.
#define ALIGN_DIST       (Params2.y) //Distance threshold under which boid aligment is applied.
#define COHESION_DIST    (Params2.z) //Distance threshold under which boid cohesion is applied.
#define MAX_FORCE        (Params2.w) //Maximum force the boid flocking algorithm can exert.

#define INVMASS          (Params3.x)  //Ivese mass of a single boid.
#define DELTATIME        (Params3.y)  //Delta time of the current frame.
#define BOUNDS           (Params3.zw) //Screen bounds in pixels.

#define MSInput          (Params4.xy) //Mouse position.
#define ALIGN_COEF       (Params4.z)  //Coefficient for alignment / velocity substraction.
#define BODYRADIUS       (Params4.w)  //Mouse gravity point radius lower bound.

#define BODYMASS         (Params5.x)  //Mouse gravity point mass,
#define DRAG             (Params5.y)  //Drag variable for (pseudo) air resistance.
#define UNITSCALE        (Params5.z)  //Scaling factor for mouse gravity point distance calculations.
#define ELASTICITY       (Params5.w)  //Elasticity of brush collisions.

//Clamps the vector v to the magnitude l
float2 mclamp(float2 v, float l) 
{
    float al = length(v);
    if (al > l) 
    {
        v = v * l / al;
    }
    return v;
}

//Returns a vecotor of direction v and the length of l
float2 maxVec(float2 v, float l) { return v * l / length(v); }

//Unpacks a 0-1 space normal into -1 - 1 space
float2 unpacknormal(float2 n) { return normalize(2 * (n - 0.5)); }

//Calculates reppelance against a single boid
float3 INS_REPEL(float3 sum, float dist, float2 deltaVec)
{
    if (dist > REPEL_DIST * REPEL_DIST) 
        return sum;

    sum.xy += deltaVec * (REPEL_DIST / dist);
    sum.z  += 1;

    return sum;
}

//Calculates alignment towards a single boid
float3 INS_ALIGN(float3 sum, float2 vel, float dist)
{
    if(dist > ALIGN_DIST * ALIGN_DIST)
        return sum;

    sum.xy += vel;
    sum.z  += 1;

    return sum;
}

//Calculates cohesion towards a single boid
float3 INS_COHESION(float3 sum, float2 pos, float dist)
{
    if(dist > COHESION_DIST * COHESION_DIST)
        return sum;

    sum.xy += pos;
    sum.z  += 1;

    return sum;
}

//Calculates a composite steering force for a single boid (repel & align)
float2 COMPOSITE_STEERING(float3 sum, float2 vel)
{
    if(sum.z <= 0.0f)
        return sum.xy;

    sum.xy = maxVec(sum.xy, ALIGN_COEF) - vel;
    sum.xy = mclamp(sum.xy, MAX_FORCE);
    return sum.xy;
}

//Calculates a composite cohesion force for a single boid
float2 COMPOSITE_COHESION(float3 sum, float2 vel, float2 pos)
{
    if(sum.z <= 0.0f)
        return sum.xy;

    sum.xy = sum.xy / sum.z; // Average Position
    sum.xy = sum.xy - pos;
    sum.xy = maxVec(sum.xy, COHESION_COEF) - vel;
    sum.xy = mclamp(sum.xy, MAX_FORCE);

    return sum.xy;
}

//Calculates the force applied to a boid based on the current boid flocking parameters
//This could be added to a conditional compile since it is very expensive.
//@TODO Consider adding inter boid collisions
float2 CALCULATE_BOID_FORCE(int id,  Boid b)
{
    float3 repel, align, cohesion = 0;

    for (int i = NumBoids - 1; i >= 0; --i)
    {
        if (id == i)
            continue;

        float2 deltaVec  = b.pos - BoidBuffer[i].pos;
        float  rDistance = deltaVec.x * deltaVec.x + deltaVec.y * deltaVec.y;

        repel    = INS_REPEL(repel,       rDistance, deltaVec);
        align    = INS_ALIGN(align,       BoidBuffer[i].vel, rDistance);
        cohesion = INS_COHESION(cohesion, BoidBuffer[i].pos, rDistance);
    }

    repel.xy    = COMPOSITE_STEERING(repel,    b.vel);
    align.xy    = COMPOSITE_STEERING(align,    b.vel);
    cohesion.xy = COMPOSITE_COHESION(cohesion, b.vel, b.pos);

    float2 force = repel.xy    * REPEL_WEIGHT +   
                   align.xy    * ALIGN_WEIGHT +   
                   cohesion.xy * COHESION_WEIGHT;

    force = mclamp(force, MAX_FORCE);

    return force;
}

//Wraps a positions beyond screen bounds
float2 WRAPBORDER(float2 pos)
{
    return float2( fmod(pos.x + BOUNDS.x, BOUNDS.x), fmod(pos.y + BOUNDS.y, BOUNDS.y));
}

//Calculates gravitational pull towards mouse point
float2 CALCULATE_GRAVITY(Boid b)
{
    float2 vec     = (b.pos - MSInput) *  UNITSCALE;
    float  rsqrtl  = vec.x * vec.x + vec.y * vec.y;

    //To avoid high gravitational forces
    //and to simulate a circle with homogenous density
    //distance is clameped to BODYRADIUS
    rsqrtl  = max(rsqrtl, BODYRADIUS * BODYRADIUS);
    rsqrtl *= rsqrtl;
    rsqrtl  = 1 / rsqrtl;

    float mass = 1 / INVMASS;

    return vec * G * (BODYMASS * mass) * rsqrtl;
}

//Calculates Acceleration, (Pseudo) Drag & delta position
void   CALCULATE_KINEMATICS(inout Boid b, float2 force)
{
    b.vel += force * DELTATIME * INVMASS;
    b.vel *= saturate(1 - DELTATIME * DRAG);
    b.pos += b.vel * DELTATIME;
}

//Solves collisions when boid touches a pixel that has been drawn onto by the normal painter
//The collision masks channel z indicates the distance the boid needs to be moved for full depenetration
//If z is greater than 0 the boid is inside a brush stroke
//To solve the collision the boid is moved along the unpacked normal multiplied by z
//Boid velocity is reflected from the normal plane and multiplied by ELASTICITY with describes the elasticity of the collision
void   BRUSH_COLLISION(inout Boid b)
{
    float2 uvPos = b.pos.xy / BOUNDS;
    float4 collision = CollisionMask.SampleLevel(samplerCollisionMask, uvPos, 0);

    //If no collision was detected lerp the boid color towards gray
    if (collision.z <= 0)
    {
        b.col = lerp(b.col, float4(0.25, 0.5, 0.5, 1), DELTATIME * 0.01);
        return;
    }

    float2 normal = -unpacknormal(collision.xy);

    //Interpolation between perpendicular impact elasticity and aligned friction
    //For the sake of simplicity there is no static friction in the simulation
    //and only very lazy dynamic one.
    float conservation = dot(normalize(-b.vel), normal);
    conservation = lerp(DYNAMIC_FRICTION_COEF, ELASTICITY, saturate(conservation));

    b.vel = reflect(b.vel, normal) * conservation;
    b.pos += normal * collision.z;
    b.col  = float4(1, 0, 0, 1);
}

//Main method of the compute shader
//Due to the way shaders are parsed this needs to be placed last
//so it can reference the previously declared methods.
//Maximum thread group size on nvidia gpus was 32, though luck amd users.
[numthreads(32, 1, 1)] 
void CSBoidMain(uint3 id : SV_DispatchThreadID) 
{
    Boid b = BoidBuffer[id.x];

    float2 force  = CALCULATE_BOID_FORCE((int) id.x, b);
           force += CALCULATE_GRAVITY(b);

    CALCULATE_KINEMATICS(b, force);

    b.pos = WRAPBORDER(b.pos);

    BRUSH_COLLISION(b);

    BoidBuffer[id.x] = b;
}